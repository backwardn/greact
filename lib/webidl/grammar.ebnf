definitions = {extendedAttributeList definition}  | empty .
definition =
	 callbackOrInterface
	| partial
	| dictionary
	| enum_
	| typedef
	| implementsStatement .

callbackOrInterface =
	 "callback" callbackRestOrInterface
	| interface_
	| class_ .

callbackRestOrInterface =
	 callbackRest
	| interface_ .

interface_ =
	 "interface" identifier inheritance "{" interfaceMembers "}" ";" .

class_ =
     "class" identifier extension "{" interfaceMembers "}" ";" .

partial =
	 "partial" partialDefinition .

partialDefinition =
	 partialInterface
	| partialDictionary .

partialInterface =
	 "interface" identifier "{" interfaceMembers "}" ";" .

interfaceMembers =
	 extendedAttributeList interfaceMember interfaceMembers
	| empty .

interfaceMember =
	 const_
	| operation
	| serializer
	| stringifier
	| staticMember
	| iterable
	| readonlyMember
	| readWriteAttribute
	| readWriteMaplike
	| readWriteSetlike .

dictionary =
	 "dictionary" identifier inheritance "{" dictionaryMembers "}" ";" .

dictionaryMembers =
	 extendedAttributeList dictionaryMember dictionaryMembers
	| empty .

dictionaryMember =
	 required type identifier default_ ";" .

required =
	 "required"
	| empty .

partialDictionary =
	 "dictionary" identifier "{" dictionaryMembers "}" ";" .

default_ =
	 "=" defaultValue
	| empty .

defaultValue =
	 constValue
	| string_lit
	| "[" "]" .

inheritance =
	 ":" identifier
	| empty .


extension =
     "extends" identifier
    | empty .

enum_ =
	 "enum" identifier "{" enumValueList "}" ";" .

enumValueList =
	 string_lit enumValueListComma .

enumValueListComma =
	 "," enumValueListString 
	| empty .

enumValueListString =
	 string_lit enumValueListComma
	| empty .

callbackRest =
	 identifier "=" returnType "(" argumentList ")" ";" .

typedef =
	 "typedef" type identifier ";" .

implementsStatement =
	 identifier "implements" identifier ";" .

const_ =
	 "const" constType identifier "=" constValue ";" .

constValue =
	 booleanLiteral
	| floatLiteral
	| int_lit
	| "null" .

booleanLiteral =
	 "true"
	| "false" .

floatLiteral =
	 float_lit
	| "-Infinity"
	| "Infinity"
	| "NaN" .

serializer =
	 "serializer" serializerRest .

serializerRest =
	 operationRest
	| "=" serializationPattern ";"
	| ";" .

serializationPattern =
	 "{" serializationPatternMap "}"
	| "[" serializationPatternList "]"
	| identifier .

serializationPatternMap =
	 "getter"
	| "inherit" identifiers
	| identifier identifiers
	| empty .

serializationPatternList =
	"getter"
	| identifier identifiers
	| empty .

stringifier =
	 "stringifier" stringifierRest .

stringifierRest =
	 readOnly attributeRest
	| returnType operationRest
	| ";" .

staticMember =
	 "static" staticMemberRest .

staticMemberRest =
	 readOnly attributeRest
	| returnType operationRest .

readonlyMember =
	 "readonly" readonlyMemberRest .

readonlyMemberRest =
	 attributeRest
	| readWriteMaplike
	| readWriteSetlike .

readWriteAttribute =
	 "inherit" readOnly attributeRest
	| attributeRest .

attributeRest =
	"attribute" type attributeName ";" .

attributeName =
	 attributeNameKeyword
	| identifier .

attributeNameKeyword =
	 "required" .


readOnly =
	 "readonly"
	| empty .

operation =
	 returnType operationRest
	| specialOperation .

specialOperation =
	 special specials returnType operationRest .


specials =
	 special specials
	| empty .

special =
	 "getter"
	| "setter"
	| "deleter"
	| "legacycaller" .

operationRest =
	 optionalIdentifier "(" argumentList ")" ";" .

optionalIdentifier =
	 identifier
	| empty .

argumentList =
	 argument arguments
	| empty .

arguments =
	 "," argument arguments
	| empty .

argument =
	 extendedAttributeList optionalOrRequiredArgument .

optionalOrRequiredArgument =
	 "optional" type argumentName default_
	| type ellipsis argumentName .

argumentName =
	 argumentNameKeyword
	| identifier .

ellipsis =
	 "..."
	| empty .

iterable =
	 "iterable" "<" type optionalType ">" ";" .

optionalType =
	 "," type
	| empty .

readWriteMaplike =
	maplikeRest .

readWriteSetlike =
	 setlikeRest .

maplikeRest =
	 "maplike" "<" type "," type ">" ";" .

setlikeRest =
	 "setlike" "<" type ">" ";" .

extendedAttributeList =
	 "[" extendedAttribute extendedAttributes "]"
	| empty .

extendedAttributes =
	 "," extendedAttribute extendedAttributes
	| empty .

extendedAttribute =
	 "(" extendedAttributeInner ")" extendedAttributeRest
	| "[" extendedAttributeInner "]" extendedAttributeRest
	| "{" extendedAttributeInner "}" extendedAttributeRest
	| other extendedAttributeRest .

extendedAttributeRest =
	 extendedAttribute
	| empty .

extendedAttributeInner =
	 "(" extendedAttributeInner ")" extendedAttributeInner
	| "[" extendedAttributeInner "]" extendedAttributeInner
	| "{" extendedAttributeInner "}" extendedAttributeInner
	| otherOrComma extendedAttributeInner
	| empty .


other =
	 int_lit
	| float_lit
	| identifier
	| string_lit
	| "-"
	| "-Infinity"
	| "."
	| "..."
	| ":"
	| ";"
	| "<"
	| "="
	| ">"
	| "?"
	| "ByteString"
	| "DOMString"
	| "FrozenArray"
	| "Infinity"
	| "NaN"
	| "RegExp"
	| "USVString"
	| "any"
	| "boolean"
	| "byte"
	| "double"
	| "false"
	| "float"
	| "long"
	| "null"
	| "object"
	| "octet"
	| "or"
	| "optional"
	| "sequence"
	| "short"
	| "true"
	| "unsigned"
	| "void"
	| argumentNameKeyword
	| bufferRelatedType .

argumentNameKeyword =
	 "attribute"
	| "callback"
	| "const"
	| "deleter"
	| "dictionary"
	| "enum"
	| "getter"
	| "implements"
	| "inherit"
	| "interface"
	| "iterable"
	| "legacycaller"
	| "maplike"
	| "partial"
	| "required"
	| "serializer"
	| "setlike"
	| "setter"
	| "static"
	| "stringifier"
	| "typedef"
	| "unrestricted" .

otherOrComma =
	 other
	| "," .

type = 
    singleType
    | unionType null_ .

singleType = 
    nonAnyType 
    | "any" .

unionType = 
    "(" unionMemberType "or" unionMemberType unionMemberTypes ")" .

unionMemberType = 
    nonAnyType
    | unionType null_ .

unionMemberTypes = 
    "or" unionMemberType unionMemberTypes 
    | empty .

empty = /* empty */ .
nonAnyType =
     primitiveType  null_
    | promiseType null_
    |"ByteString"  null_ 
	|"DOMString"  null_ 
	| "USVString"  null_ 
	| identifier  null_ 
	| "sequence" "<" type ">" null_ 
	| "object"  null_ 
	| "RegExp"  null_ 
	| "DOMException"  null_ 
	| bufferRelatedType  null_ 
	| "FrozenArray" "<" type ">" null_ .

bufferRelatedType = "ArrayBuffer"
    | "DataView"
	| "Int8Array"
	| "Int16Array"
	| "Int32Array"
	| "Uint8Array"
	| "Uint16Array"
	| "Uint32Array"
	| "Uint8ClampedArray"
	| "Float32Array"
	| "Float64Array" .

constType = 
    primitiveType null_ 
    | identifier null_ .

primitiveType= 
    unsignedIntegerType
	| unrestrictedFloatType
	| "boolean"
	| "byte"
	| "octet" .

unrestrictedFloatType = 
    "unrestricted" floatType
    | floatType .

floatType = 
    "float"	
    | "double" .

unsignedIntegerType = 
    "unsigned" integerType 
    | integerType .

integerType = 
    "short" 
    | "long" optionalLong .

optionalLong =
     "long" 
     | empty .
     
promiseType = "Promise" "<" returnType ">" .

null_ =
	"?" 
	| empty .

returnType =
	type 
	| "void" .


identifiers = "," identifier identifiers | empty .

newline        = /* the Unicode code point U+000A */ .
unicode_char   = /* an arbitrary Unicode code point except newline */ .
unicode_letter = /* a Unicode code point classified as "Letter" */ .
unicode_digit  = /* a Unicode code point classified as "Number, decimal digit" */ .

letter        = unicode_letter | "_" .
decimal_digit = "0" … "9" .
octal_digit   = "0" … "7" .
hex_digit     = "0" … "9" | "A" … "F" | "a" … "f" .

identifier = letter { letter | unicode_digit } .

int_lit     = decimal_lit | octal_lit | hex_lit .
decimal_lit = ( "1" … "9" ) { decimal_digit } .
octal_lit   = "0" { octal_digit } .
hex_lit     = "0" ( "x" | "X" ) hex_digit { hex_digit } .

float_lit = decimals "." [ decimals ] [ exponent ] |
            decimals exponent |
            "." decimals [ exponent ] .
decimals  = decimal_digit { decimal_digit } .
exponent  = ( "e" | "E" ) [ "+" | "-" ] decimals .


unicode_value    = unicode_char | little_u_value | big_u_value | escaped_char .
byte_value       = octal_byte_value | hex_byte_value .
octal_byte_value = `\` octal_digit octal_digit octal_digit .
hex_byte_value   = `\` "x" hex_digit hex_digit .
little_u_value   = `\` "u" hex_digit hex_digit hex_digit hex_digit .
big_u_value      = `\` "U" hex_digit hex_digit hex_digit hex_digit
                           hex_digit hex_digit hex_digit hex_digit .
escaped_char     = `\` ( "a" | "b" | "f" | "n" | "r" | "t" | "v" | `\` | "'" | `"` ) .

string_lit             = raw_string_lit | interpreted_string_lit .
raw_string_lit         = "`" { unicode_char | newline } "`" .
interpreted_string_lit = `"` { unicode_value | byte_value } `"` .

